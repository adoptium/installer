// Plug-in order matters. The groovy plug-in must appear after java-gradle-plugin or some tests
// won't be picked up.
plugins {
    id "checkstyle"
    id "java-gradle-plugin"
    id "groovy"
}

ext {
    assertj_version = "3.14.0"
    junit_version = "5.4.2"
    okhttp_version = "3.14.1"
}

repositories {
    mavenCentral()
    jcenter {
        content {
            includeGroup "org.jfrog.artifactory.client"
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

checkstyle {
    configFile = file("checkstyle.xml")
    ignoreFailures false
    showViolations true
    checkstyleMain.exclude "**/CreatePackageJson.java", "**/UpdatePackageJson.java"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functTest/groovy')
        resources.srcDir file('src/functTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

check.dependsOn functionalTest

dependencies {
    implementation "com.samskivert:jmustache:1.14"
    implementation "org.jetbrains:annotations:17.0.0"
    implementation "org.jfrog.artifactory.client:artifactory-java-client-services:2.8.3"

    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"

    functionalTestImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    functionalTestImplementation "org.assertj:assertj-core:$assertj_version"
    functionalTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
}
