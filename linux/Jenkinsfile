/*
--------------------------------------------
buildArch   RPM         DEB         Comments
--------------------------------------------
x86_64      x86_64      amd64
armv7l      armv7hl     armhf       arm32
aarch64     aarch64     arm64       arm64
ppc64le     ppc64le     ppc64le
source      src         -           only for SRPM and no need specify as option or target
s390x       s390x       s390x       only for jdk8+
*/

env.NODE_LABEL = 'dockerBuild&&linux&&x64' // Default node and also used for build RedHat + Suse + Debian x64
env.PRODUCT = 'temurin'

pipeline {
    agent {
        label NODE_LABEL
    }
    options {
        timeout(time: 2, unit: 'HOURS')
    }
    parameters {
        choice(name: 'VERSION', choices: ['8', '11', '17', '19'], description: 'Build for specific JDK VERSION')
        choice(name: 'ARCH', choices: ['x86_64', 'armv7l', 'aarch64', 'ppc64le', 's390x', 'all'], description: 'Build for specific platform\n s390x not for VERSION 8\n')
        choice(name: 'DISTRO', choices: ['all', 'Alpine', 'Debian', 'RedHat', 'Suse'], description: 'Build for specific Distro\n Select RPM builds for RedHat and Suse')
        booleanParam(name: 'uploadPackage', defaultValue: false, description: 'Tick this box to upload the deb/rpm files (exclude src.rpm) to Artifactory for official release')
        booleanParam(name: 'uploadSRCRPM', defaultValue: false, description: 'Tick this box to upload (src.rpm files) to Artifactory')
        string(name: 'gitrepo', defaultValue: 'https://github.com/adoptium/installer', description: 'Do not modify unless want to build from a specific gitrepo')
        string(name: 'gitbranch', defaultValue: 'master', description: 'Do not change unless want to build on a specific branch from above gitrepo')
        booleanParam(name: 'enableDebug', defaultValue: false, description: 'Tick to enable --stacktrace for gradle build')
    }
    tools {
        // this is set in the jenkins global tool
        jdk 'jdk-11.0.13+8'
    }
    stages {
        stage('Prepare Build') {
            steps {
                script {
                    currentBuild.displayName =  "jdk${VERSION} - ${ARCH} - ${DISTRO}"
                    currentBuild.description = env.BUILD_USER_ID
                    sh('docker --version')
                }
                checkout(
                    [
                        $class: 'GitSCM',
                        branches: [[name: "$gitbranch"]],
                        extensions: [[$class: 'CleanBeforeCheckout', deleteUntrackedNestedRepositories: true]],
                        userRemoteConfigs: [[url: "$gitrepo"]]
                    ]
                )
                dir('linux') {
                    stash name: 'installercode', includes: '**'
                }
            }
        }
        stage('BUILD')
        {
            parallel{
                stage('Build Installer for Alpine') {
                    when  {
                        beforeAgent true
                        anyOf {
                            expression { params.DISTRO == 'all' }
                            expression { params.DISTRO == 'Alpine' } // only trigger debian build
                        }
                    }
                    steps{
                        dir('linuxDebian') {
                            script {
                                jenkinsStepNonDeb("Alpine")
                            }
                        }
                    }
                }
                stage('Build Installer for Debian') {
                    when  {
                        beforeAgent true
                        anyOf {
                            expression { params.DISTRO == 'all' }
                            expression { params.DISTRO == 'Debian' } // only trigger debian build
                        }
                    }
                    // specific jenkins agent will be assigned inside of jenkinsSepDeb() per ARCH
                    steps {
                        dir('linuxDebian') {
                            script {
                                jenkinsStepDeb()
                            }
                        }
                    }
                }
                stage('Build Installer for Redhat') {
                    when  {
                        beforeAgent true
                        anyOf {
                            expression { params.DISTRO == 'all' }
                            expression { params.DISTRO == 'RedHat' }  // only trigger redhat build
                        }
                    }
                    steps {
                        dir('linuxRedHat') {
                            script {
                                DISTRO = 'RedHat'
                                jenkinsStepNonDeb('RedHat')
                            }
                        }
                    }
                }
                stage('Build Installer for Suse') {
                    when  {
                        beforeAgent true // do condition when before allocate to agent
                        anyOf {
                            expression { params.DISTRO == 'all' }
                            expression { params.DISTRO == 'Suse' } // only trigger suse build
                        }
                    }
                    steps {
                        dir('linuxSuse') {
                            script {
                                jenkinsStepNonDeb('Suse')
                            }
                        }
                    }
                }
            }
        }
    }
}

/*
* Common Functions
*/

// function only handle debian as DISTRO
def jenkinsStepDeb() {
    echo "Installer Job for Temurin jdk ${VERSION} - ${ARCH} - Debian"
    //make sure this is an array not a string
    def debArchAllList = []
    // for one single ARCH add into array
    debArchAllList.add("${ARCH}")
    // when ARCH = all, rewrite list
    if ("${ARCH}" == 'all') {
        debArchAllList = ['x86_64', 'armv7l', 'aarch64', 'ppc64le', 's390x']
    }
    debArchAllList.each { DebARCH ->
        // special handle: no label x86_64 only x64 for debian agent
        def debLabel = "${DebARCH}&&docker"
        if ("${DebARCH}" == 'x86_64') {
            debLabel = 'x64&&dockerBuild'
        }
        // reallocate jenkins agent per element in list
        node("linux&&${debLabel}") {
            setup('Debian', "${DebARCH}")
            unstash 'installercode'
            buildAndTest('Debian', "${DebARCH}")
            if (params.uploadPackage.toBoolean()) {
                echo "Upload artifacts for ${VERSION} - ${DebARCH} - Debian"
                uploadArtifacts('Debian', "${DebARCH}")
            }
        }
    }
}

// function handle both Alpine, RedHat and Suse as DISTRO
def jenkinsStepNonDeb(DISTRO) {
    echo "Installer Job for Temurin jdk ${VERSION} - ${ARCH} - ${DISTRO}"
    setup("${DISTRO}", "${ARCH}")
    unstash 'installercode'
    buildAndTest("${DISTRO}", "${ARCH}")
    if (params.uploadPackage.toBoolean()) {
        echo "Upload artifacts for ${VERSION} - ${ARCH} - ${DISTRO}"
        uploadArtifacts("${DISTRO}", "${ARCH}")
    }
}

// common function regardless DISTRO
def setup(DISTRO, buildArch) {
    cleanWs()
    // Docker --mount option requires BuildKit
    env.DOCKER_BUILDKIT = 1
    env.COMPOSE_DOCKER_CLI_BUILD = 1
    env._JAVA_OPTIONS = (buildArch == 'armv7l' && DISTRO == 'Debian') ? '' : '-Xmx4g'
}

// common function regardless DISTRO
def buildAndTest(DISTRO, buildArch) {
    try {
        if (DISTRO != "Debian") { // for RPM based: RedHat / Suse / Alpine
            def privateKey = 'adoptium-artifactory-gpg-key'
            if (DISTRO == "Alpine") {
                privateKey = 'adoptium-artifactory-rsa-key'
            }
            // Install Adoptium GPG key for RPM signing
            withCredentials([file(credentialsId: privateKey, variable: 'GPG_KEY')]) {
                def buildCLI = "./gradlew packageJdk${DISTRO} checkJdk${DISTRO} --parallel -PPRODUCT=${env.PRODUCT} -PPRODUCT_VERSION=${VERSION} -PGPG_KEY=${GPG_KEY} -PARCH=${buildArch}"
                buildCLI = params.enableDebug.toBoolean() ? buildCLI + ' --stacktrace' : buildCLI
                sh("$buildCLI")
            }
        } else {
            def gBuildTask = (buildArch == 'x86_64') ? "packageJdk${DISTRO} checkJdk${DISTRO}" : "packageJdk${DISTRO}"
            def debArchList = [
                'x86_64' : 'amd64',
                'armv7l': 'armhf',
                'aarch64': 'arm64',
                'ppc64le': 'ppc64el',
                's390x'  : 's390x'
            ]
            def buildCLI = "./gradlew ${gBuildTask} --parallel -PPRODUCT=${env.PRODUCT} -PPRODUCT_VERSION=${VERSION} -PARCH=${debArchList[buildArch]}"
            buildCLI = params.enableDebug.toBoolean() ? buildCLI + ' --stacktrace' : buildCLI
            sh("$buildCLI")
        }
    } catch (Exception ex) {
        echo 'Exception in buildAndTest: ' + ex
        currentBuild.result = 'FAILURE'  // set the whole pipeline 'red' if build or test fail. Do not use "error" that will not call "finally"
    } finally {
        // should not allow empty archive, otherwsie nothing created in the previous step: package and test result( not needed )
        archiveArtifacts artifacts: '**/build/ospackage/*,**/build/reports/**,**/packageTest/dependencies/deb/*', onlyIfSuccessful:false, allowEmptyArchive: false
    }
}

def uploadArtifacts(DISTRO, buildArch) {
    switch(DISTRO) {
        case "Debian":
            uploadDebArtifacts(buildArch);
            break;
        case "Alpine":
            uploadAlpineArtifacts(buildArch);
            break;
        default:
            uploadRpmArtifacts(DISTRO,buildArch);
            break;
    }
}

def uploadAlpineArtifacts(buildArch) {
    // currently only support x64 as buildArch
    rtUpload ( //artifactory plugin
        serverId: 'adoptium.jfrog.io',
        failNoOp: true,
        spec: """{
            "files": [
                {
                "pattern": "**/build/ospackage/temurin-*.apk",
                "target": "apk/alpine/main/${buildArch}/"
                }
            ]
        }""",
    )
}

def uploadDebArtifacts(buildArch) {
    // full list of all platforms, up to user to opt out s390x+jdk8
    def debArchList = [
        'x86_64' : 'amd64',
        'armv7l': 'armhf',
        'aarch64': 'arm64',
        'ppc64le': 'ppc64el',
        's390x'  : 's390x'
    ]
    /*
        Debian/Ubuntu   10.0       11.0        16.04     20.04    22.04    22.10
        add more into list when avaiable for release
        also update linux/jdk/debian/main/packing/build.sh
    */
    def deb_versions = ['buster', 'bullseye', 'bionic', 'focal', 'jammy', 'kinetic']
    def distro_list = ''
    deb_versions.each { deb_version ->
        // Creates list like deb.distribution=stretch;deb.distribution=buster;
        distro_list += "deb.distribution=${deb_version};"
    }
    
    for (int i = 0; i < 5; i++) { // loop 5 times at most
        echo "Prepare for ${debArchList[buildArch]}.deb"
        if(findFiles(glob: "**/build/ospackage/temurin-*${debArchList[buildArch]}.deb").length == 0) {
            /*
            workaround for error when upload to artifactory: No files were affected in the upload process
            might be caused by slow archive done by jenkins
            */
            sleep 2
        } else {
            rtUpload( //artifactory plugin
                serverId: 'adoptium.jfrog.io',
                failNoOp: true,
                spec: """{
                    "files": [
                        {
                        "pattern": "**/build/ospackage/temurin-*${debArchList[buildArch]}.deb",
                        "target": "deb/pool/main/t/temurin-${VERSION}/",
                        "props": "${distro_list}deb.component=main;deb.architecture=${debArchList[buildArch]}"
                        }
                    ]
                }""",
            )
            break
        }
    }
}

def uploadRpmArtifacts(DISTRO, rpmArch) {
    def distro_Package = [
        'redhat' : [
            'rpm/centos/7',
            'rpm/rocky/8',
            'rpm/rhel/7',
            'rpm/rhel/8',
            'rpm/rhel/9',
            'rpm/fedora/35',
            'rpm/fedora/36',
            'rpm/oraclelinux/7',
            'rpm/oraclelinux/8',
            'rpm/amazonlinux/2'

        ],
        'suse'   : [
            'rpm/opensuse/15.3',
            'rpm/opensuse/15.4',
            'rpm/sles/12',
            'rpm/sles/15'
        ]
    ]
    def packageDirs = distro_Package[DISTRO.toLowerCase()]
    // full list of all platforms, up to user to opt out s390x+jdk8 from input
    def rpmArchList=[
        'x86_64' : 'x86_64',
        'armv7hl': 'armv7hl',
        'aarch64': 'aarch64',
        'ppc64le': 'ppc64le',
        's390x'  : 's390x'
    ]
    if ("${rpmArch}" != 'all') {
        // when only build and upload one arch, reset
        rpmArchList = ["${rpmArch}": "${rpmArchList[rpmArch]}"]
    }
    // Enable upload src.rpm
    if ( params.uploadSRCRPM.toBoolean() || params.DISTRO == 'all' ) {
        rpmArchList.put('source', 'src')
    }

    packageDirs.each { packageDir ->
        rpmArchList.each { entry ->
            for (int i = 0; i < 5; i++) { // loop 5 times at most
                echo "Prepare for ${entry.value}.rpm"
                if(findFiles(glob: "**/build/ospackage/*.${entry.value}.rpm").length == 0) {
                   /*
                    workaround for error when upload to artifactory: No files were affected in the upload process
                    might be caused by slow archive done by jenkins
                    */
                    sleep 2
                } else {
                    rtUpload(
                        serverId: 'adoptium.jfrog.io',
                        failNoOp: true,
                        spec: """{
                            "files": [
                                {
                                "pattern": "**/build/ospackage/*.${entry.value}.rpm",
                                "target": "${packageDir}/${entry.key}/Packages/"
                                }
                            ]
                        }"""
                    )
                    break
                }
            }
        }
    }
}
